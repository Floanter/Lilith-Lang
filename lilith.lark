start               : block+

block               : import
                    | c_import
                    | return
                    | macro
                    | conditional
                    | loop
                    | struct
                    | calls
                    | assign 
                    | /empty/ ";"   -> empty

import              : "spell" string ";"             -> getspell
                    | "spell" "<" identifier ">" ";" -> getlibspell

c_import            : "c_header" string ";"     -> c_header
                    | "c_lib" "<" identifier ">" ";"    -> c_lib

return              : "return" value ";"

macro               : "newmacro" identifier value "end" -> inlinemacro
                    | "newmacro" identifier "(" value ")" "do" block+ "end" -> fullmacro

conditional         : "if" condition (logicaloperator condition)* "then" (block | specialword)+ "end"                                   -> ifcondition
                    | "if" condition (logicaloperator condition)* "then" (block | specialword)+ "else" (block | specialword)+ "end"     -> ifelsecondition
                    | "unless" condition (logicaloperator condition)* "then" (block | specialword)+ "end"                               -> unlesscondition
                    | "unless" condition (logicaloperator condition)* "then" (block | specialword)+ "else" (block | specialword)+ "end" -> unlesselsecondition
                    | "switch" value whenstatement* defaultstatement "end"                                                              -> switchcondition

specialword         : /done/ ";"    -> break
                    | /skip/ ";"    -> continue

loop                : "while" condition (logicaloperator condition)* "do" block+ "end"  -> whileloop
                    | "for" assign condition ";" assign "do" block+ "end"               -> forloop

struct              : "struct" identifier assign+ "end"

calls               : call ("." call)* ";"

call                : identifier "!"                    -> callnoparams
                    | identifier "(" parameters ")"     -> callwithparams

assign              : "void" "main" "->" block+ "end"                                           -> mainfunction
                    | type identifier "->" block+ "end"                                         -> functionnoarguments
                    | type identifier "->" "(" arguments ")" block+ "end"                       -> functionwitharguments
                    | "newtype" type identifier ";"                                             -> typedef
                    | identifier ("." assign)+                                                  -> structvariableaccess
                    | identifier arrayposition+ "+" "+" ";"                                     -> arrayvariableincrement
                    | identifier arrayposition+ "-" "-" ";"                                     -> arrayvariabledecrement
                    | identifier "+" "+" ";"                                                    -> variableincrement
                    | identifier "-" "-" ";"                                                    -> variabledecrement
                    | identifier arrayposition+ assignoperator value ";"                        -> arrayvariablereasign
                    | identifier assignoperator value ";"                                       -> variablereasign
                    | "array" "<" type ">" identifier "=" "[" value ("," value)* "]" ";"        -> arrayvariableassign
                    | "array" "<" type ">" identifier arrayposition+ ";"                        -> variabledeclaration
                    | type identifier ";"                                                       -> variabledeclaration
                    | type identifier "=" value ";"                                             -> variableassing
                    | "const" type identifier "=" value ";"                                     -> constvariable
                    | "shared" type identifier "=" value ";"                                    -> sharedvariable
                    | "shared" "const" type identifier "=" value ";"                            -> sharedconstvariable

whenstatement       : "when" value ":" block+ "done" ";"

defaultstatement    : "default" ":" block+ 

arrayposition       :  "[" number "]"

condition           : value comparisonoperator value
                    | value

assignoperator      : /\=/
                    | /\+\=/
                    | /\-\=/
                    | /\*\=/
                    | /\/\=/
                    | /\%\=/

comparisonoperator  : /is/      
                    | /not/      
                    | /greater/  
                    | /less/     
                    | /\>\=/    
                    | /\<\=/     

logicaloperator     : /and/
                    | /or/

parameters          : value ("," value)*

arguments           : argument ("," argument)*

argument            : /void/
                    | type identifier
                    | type identifier "=" value         -> argumentdefault
                    | "array" "<" type ">" identifier   -> arrayargument


identifier          : /[\&\_][a-zA-Z0-9]*/
                    | CNAME

type                : /[a-zA-Z0-9]+[\*]?/ -> type

value               : string
                    | number
                    | bool
                    | arithmetic
                    | calls
                    | identifier

arithmetic          : value (arithmeticsign value)+

arithmeticsign      : /\+/
                    | /\-/ 
                    | /\//
                    | /\*/ 
                    | /\%/ 

string              : /\'.*\'/ 
                    | /\".*\"/

number              : SIGNED_NUMBER
                    | SIGNED_FLOAT

bool                : /true/
                    | /false/

COMMENT             : "--" /[^\n]/*

%import common.SIGNED_NUMBER
%import common.SIGNED_FLOAT
%import common.ESCAPED_STRING
%import common.CNAME
%import common.WS

%ignore COMMENT
%ignore WS